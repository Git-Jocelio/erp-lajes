unit ufrmCartaoVendasE;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, ufrmBaseEdicao, FireDAC.Stan.Intf,
  FireDAC.Stan.Option, FireDAC.Stan.Param, FireDAC.Stan.Error, FireDAC.DatS,
  FireDAC.Phys.Intf, FireDAC.DApt.Intf, FireDAC.Stan.Async, FireDAC.DApt,
  Data.DB, FireDAC.Comp.DataSet, FireDAC.Comp.Client, Vcl.StdCtrls, Vcl.Buttons,
  Vcl.ExtCtrls, uTipos, Vcl.ComCtrls, Vcl.Mask, Vcl.DBCtrls, uBiblioteca,
  Vcl.Grids, Vcl.DBGrids, uFinanceiro, udmConn, unit_funcoes,
  ufrmSelecionaPessoa_Custo;

type
  TfrmCartaoVendasE = class(TfrmBaseEdicao)
    Label2: TLabel;
    lbl_Id: TLabel;
    Label4: TLabel;
    lbl_cadastrado_em: TLabel;
    Label5: TLabel;
    dtp_data_venda: TDateTimePicker;
    Label1: TLabel;
    lbl_alterado_em: TLabel;
    Label3: TLabel;
    cbx_operadora: TDBLookupComboBox;
    edt_id_operadora: TDBEdit;
    Label6: TLabel;
    cbx_nr_maquina: TDBLookupComboBox;
    edt_id_maquina: TDBEdit;
    edt_com_quem_ta_a_maquina: TDBEdit;
    Label7: TLabel;
    cbx_desc_bandeira: TDBLookupComboBox;
    edt_id_bandeira: TDBEdit;
    Label8: TLabel;
    cbx_desc_forma_pagamento: TDBLookupComboBox;
    edt_id_forma_pagto: TDBEdit;
    Label9: TLabel;
    edt_vr_venda: TEdit;
    Label10: TLabel;
    Label11: TLabel;
    edt_vr_liquido: TEdit;
    Label12: TLabel;
    edt_cv: TEdit;
    ds_operadora: TDataSource;
    qry_operadora: TFDQuery;
    ds_maquina: TDataSource;
    qry_maquina: TFDQuery;
    edt_dona_maquina: TDBEdit;
    Label13: TLabel;
    Label14: TLabel;
    ds_bandeiras: TDataSource;
    qry_bandeiras: TFDQuery;
    ds_forma_pagto: TDataSource;
    qry_forma_pagto: TFDQuery;
    edt_taxa: TEdit;
    gb_parcelas: TGroupBox;
    dbg_parcelas: TDBGrid;
    mtb_parcelas: TFDMemTable;
    ds_parcelas: TDataSource;
    mtb_parcelasDATA: TDateField;
    mtb_parcelasVALOR: TFloatField;
    ds_vendedor: TDataSource;
    qry_vendedor: TFDQuery;
    mtb_parcelasPARCELA: TAutoIncField;
    qry_forma_pagtoID: TIntegerField;
    qry_forma_pagtoDESCRICAO: TStringField;
    qry_forma_pagtoQTDE_PARCELAS: TIntegerField;
    procedure FormCreate(Sender: TObject);
    procedure FormShow(Sender: TObject);
    procedure cbx_operadoraCloseUp(Sender: TObject);
    procedure edt_vr_vendaExit(Sender: TObject);
    procedure btnOkClick(Sender: TObject);
    procedure edt_vr_vendaKeyPress(Sender: TObject; var Key: Char);
    procedure cbx_desc_forma_pagamentoExit(Sender: TObject);
    procedure cbx_operadoraExit(Sender: TObject);
    procedure cbx_desc_bandeiraExit(Sender: TObject);

  private
    FOperacao: uTipos.TOperacao;
    FTabela: string;
    FCodigo: integer;
    Fp_banco_id: integer;
    Fp_conta_id: integer;
    Fp_taxa: double;
    Fp_primeiro_vencimento: integer;
    Fp_proprietario_id: integer;
    Fp_somente_leitura: boolean;
    Fp_qtde_parcelas: integer;
    Fp_nm_operadora: string;
    Fp_nm_bandeira: string;
    procedure prc_busca_taxa;
    procedure prc_calcula_valor_liquido;
    procedure prc_calcular_parcelas(maquina_empresa: string);
    procedure prc_salvar;
    procedure prc_incluir_alterar(operacao: TOperacao);

  protected
    procedure prc_componentes;
    procedure prc_inicializar;
    procedure prc_ler_dados;
    function fnc_validar: boolean;

  public
    property Codigo   :integer read FCodigo write FCodigo;
    property Operacao :uTipos.TOperacao read FOperacao write FOperacao;
    property Tabela   :string read FTabela write FTabela;

    {banco e conta que receberam os créditos desta venda}
    property p_banco_id :integer read Fp_banco_id write Fp_banco_id;
    property p_conta_id :integer read Fp_conta_id write Fp_conta_id;
    property p_nm_operadora :string read Fp_nm_operadora write Fp_nm_operadora;
    property p_nm_bandeira :string read Fp_nm_bandeira write Fp_nm_bandeira;
    property p_taxa :double read Fp_taxa write Fp_taxa;
    property p_qtde_parcelas :integer read Fp_qtde_parcelas write Fp_qtde_parcelas;
    property p_primeiro_vencimento: integer read Fp_primeiro_vencimento write Fp_primeiro_vencimento;
    {id do proprietario da maquininha. se maior que 0 a maquininha é de algum fornecedor}
    property p_proprietario_id :integer read Fp_proprietario_id write Fp_proprietario_id;

    {esta propriedade quando true sera usado somente para leitura. será chamando
    pelo form de recibo de pagamento  }
    property p_somente_leitura :boolean read Fp_somente_leitura write Fp_somente_leitura;

  end;

  var
    frmCartaoVendasE : TfrmCartaoVendasE;

implementation


{$R *.dfm}

procedure TfrmCartaoVendasE.btnOkClick(Sender: TObject);
begin
  inherited;
  prc_salvar;
end;

procedure TfrmCartaoVendasE.prc_salvar;
begin
  if not fnc_validar then Exit;

  {se o form foi chamado pelo recibo não salva no banco}
  if p_somente_leitura = true then
  begin
    CLOSE;
    exit;
  end;


  if not Self.Conexao.InTransaction then Self.Conexao.StartTransaction;
  //***
  try
    prc_incluir_alterar(Operacao);
    if Self.Conexao.InTransaction then Self.Conexao.Commit;
    ModalResult := mrOk;
  except
    on E : Exception do
       begin
         unit_funcoes.CriarMensagem('AVISO','NÃO FOI POSSIVEL SALVAR O PEDIDO.' + slinebreak + slinebreak + E.Message);
         if Conexao.InTransaction then
           Conexao.Rollback;;
         //***
         Raise;
       end;
  end;

end;

procedure TfrmCartaoVendasE.prc_incluir_alterar(operacao: TOperacao);
var
  historico : string;
begin
  qry.sql.clear;
  if operacao = opExcluir then
  begin

    qry.SQL.Add('update '+ self.Tabela + ' set ATIVO = ''N'' where id =:id');
    qry.ParamByName('id').AsInteger := Codigo;
    qry.ExecSQL;
    exit;

  end;

  if operacao = OpIncluir then
  begin
    qry.SQL.Add('insert into ' + self.Tabela );
    qry.SQL.Add('(                         ');
    qry.SQL.Add('  CADASTRADO_EM,          ');
    qry.SQL.Add('  DATA_VENDA,             ');
    qry.SQL.Add('  CARTAO_OPERADORA_ID,    ');
    qry.SQL.Add('  CARTAO_MAQUINA_ID,      ');
    qry.SQL.Add('  CARTAO_BANDEIRA_ID,     ');
    qry.SQL.Add('  CARTAO_FORMA_PAGTO_ID,  ');
    qry.SQL.Add('  TAXA,                   ');
    qry.SQL.Add('  VALOR_VENDA,            ');
    qry.SQL.Add('  VALOR_LIQUIDO,          ');
    qry.SQL.Add('  CV                      ');
    //qry.SQL.Add('  VENDEDOR_ID             ');
    qry.SQL.Add(')                         ');
    qry.SQL.Add('VALUES                    ');
    qry.SQL.Add('(                         ');
    qry.SQL.Add('  :CADASTRADO_EM,         ');
    qry.SQL.Add('  :DATA_VENDA,            ');
    qry.SQL.Add('  :CARTAO_OPERADORA_ID,   ');
    qry.SQL.Add('  :CARTAO_MAQUINA_ID,     ');
    qry.SQL.Add('  :CARTAO_BANDEIRA_ID,    ');
    qry.SQL.Add('  :CARTAO_FORMA_PAGTO_ID, ');
    qry.SQL.Add('  :TAXA,                  ');
    qry.SQL.Add('  :VALOR_VENDA,           ');
    qry.SQL.Add('  :VALOR_LIQUIDO,         ');
    qry.SQL.Add('  :CV                     ');
    //qry.SQL.Add('  :VENDEDOR_ID            ');
    qry.SQL.Add(')                         ');
    qry.SQL.Add('returning ID              ');

    qry.ParamByName('CADASTRADO_EM').AsDate := Date;
  end
  else
  begin
    qry.SQL.Add('UPDATE                                            ');
    qry.SQL.Add(Tabela);
    qry.SQL.Add('SET                                               ');
    qry.SQL.Add('  ALTERADO_EM = :ALTERADO_EM,                     ');
    qry.SQL.Add('  DATA_VENDA = :DATA_VENDA,                       ');
    qry.SQL.Add('  CARTAO_OPERADORA_ID = :CARTAO_OPERADORA_ID,     ');
    qry.SQL.Add('  CARTAO_MAQUINA_ID = :CARTAO_MAQUINA_ID,         ');
    qry.SQL.Add('  CARTAO_BANDEIRA_ID = :CARTAO_BANDEIRA_ID,       ');
    qry.SQL.Add('  CARTAO_FORMA_PAGTO_ID = :CARTAO_FORMA_PAGTO_ID, ');
    qry.SQL.Add('  TAXA = :TAXA,                                   ');
    qry.SQL.Add('  VALOR_VENDA = :VALOR_VENDA,                     ');
    qry.SQL.Add('  VALOR_LIQUIDO = :VALOR_LIQUIDO,                 ');
    qry.SQL.Add('  CV = :CV                                        ');
    //qry.SQL.Add('  VENDEDOR_ID = :VENDEDOR_ID                      ');
    qry.SQL.Add('WHERE                                             ');
    qry.SQL.Add('  ID = :ID                                        ');
    //
    qry.ParamByName('ID').AsInteger       := Codigo;
    qry.ParamByName('ALTERADO_EM').AsDate := Date;

  end;
  //ShowMessage(QRY.SQL.Text);

  qry.ParamByName('DATA_VENDA').AsDate               := dtp_data_venda.Date;
  qry.ParamByName('CARTAO_OPERADORA_ID').AsInteger   := cbx_operadora.KeyValue;
  qry.ParamByName('CARTAO_MAQUINA_ID').AsInteger     := cbx_nr_maquina.KeyValue;
  qry.ParamByName('CARTAO_BANDEIRA_ID').AsInteger    := cbx_desc_bandeira.KeyValue;
  qry.ParamByName('CARTAO_FORMA_PAGTO_ID').AsInteger := cbx_desc_forma_pagamento.KeyValue;
  qry.ParamByName('TAXA').AsFloat                    := strtofloat(edt_taxa.Text);
  qry.ParamByName('VALOR_VENDA').AsFloat             := strtofloat(edt_vr_venda.Text);
  qry.ParamByName('VALOR_LIQUIDO').AsFloat           := strtofloat(edt_vr_liquido.Text);
  qry.ParamByName('CV').AsString                     := edt_cv.Text;
  //qry.ParamByName('VENDEDOR_ID').Asinteger           := uBiblioteca.SeSenao(qry_maquina.FieldByName('maquina_da_empresa').AsString = 'S', -1, cbx_vendedor.KeyValue );


  if operacao = opincluir then
  begin
    qry.Open;
    codigo := qry.FieldByName('ID').AsInteger;
  end
  else
    qry.ExecSQL;

  {cadastrar parcelas no contas a receber}
  mtb_parcelas.First;
  while not mtb_parcelas.eof do
  begin
    uFinanceiro.AddContasReceber(conexao,
                                 codigo,
                                 dtp_data_venda.date,
                                 sesenao(p_proprietario_id < 0, cbx_operadora.KeyValue{operadora},p_proprietario_id),
                                 tabela,
                                 Codigo,
                                 mtb_parcelas.fieldbyname('parcela').AsInteger,
                                 mtb_parcelas.RecordCount,
                                 strtofloat(edt_vr_liquido.Text),
                                 0,0,0,0,
                                 dtp_data_venda.date,
                                 0,
                                 0,
                                 'VENDA COM CARTÃO DE CRÉDITO CV ' + edt_cv.Text + ' PARCELA ' + mtb_parcelas.fieldbyname('parcela').AsString + '/' + inttostr(mtb_parcelas.RecordCount),
                                 '',
                                 10 //plano de contas : cartão de crédito
                                 );

    mtb_parcelas.Next;
  end;

  {se for maquina de fornecedor gerar uma conta a pagar pro vendedor}
  if p_proprietario_id > 0 then
  begin
    try
      if frmSelecionaPessoa_Custo = nil then
        frmSelecionaPessoa_Custo := TfrmSelecionaPessoa_Custo.Create(application);

      frmSelecionaPessoa_Custo.ShowModal;
      // pega dados do formulario

      if frmSelecionaPessoa_Custo.rg_opcoes.ItemIndex = 0 then
      begin

        historico := '[ MAQUININHA ' + edt_dona_maquina.Text + ' - ' + cbx_desc_forma_pagamento.Text + ' ] CV. ' + edt_cv.Text + ' ' + frmSelecionaPessoa_Custo.pessoa_nome + ' ' + frmSelecionaPessoa_Custo.pessoa_nome + ' ' + frmSelecionaPessoa_Custo.pessoa_historico;

      end else
      if frmSelecionaPessoa_Custo.rg_opcoes.ItemIndex = 1 then
      begin

        historico := '[ MAQUININHA ' + edt_dona_maquina.Text + ' - ' + cbx_desc_forma_pagamento.Text + ' ] CV. ' + edt_cv.Text + ' PED. N. ' +  frmSelecionaPessoa_Custo.pessoa_historico;

      end else
      if frmSelecionaPessoa_Custo.rg_opcoes.ItemIndex = 2 then
      begin

        historico := '[ MAQUININHA ' + edt_dona_maquina.Text + ' - ' + cbx_desc_forma_pagamento.Text + ' ] CV. ' + edt_cv.Text + ' ' +  frmSelecionaPessoa_Custo.pessoa_historico;

      end;



      ufinanceiro.prc_contas_pagar(Opincluir, conexao, -1, -1, 14,
                                frmSelecionaPessoa_Custo.pessoa_id,
                                inttostr(codigo),historico, 'CARTAO_VENDAS', codigo,
                                1, 1, strtofloat(edt_vr_venda.Text),
                                0,0,0,0,
                                date , 0,'S','S','S','S', '', ''
                                 );
    finally
      freeandnil(frmSelecionaPessoa_Custo);
    end;

  end;
end;

procedure TfrmCartaoVendasE.cbx_desc_bandeiraExit(Sender: TObject);
begin
  p_nm_bandeira := cbx_desc_bandeira.Text;
end;

procedure TfrmCartaoVendasE.cbx_desc_forma_pagamentoExit(Sender: TObject);
begin
  p_qtde_parcelas := qry_forma_pagto.FieldByName('QTDE_PARCELAS').AsInteger;
end;

procedure TfrmCartaoVendasE.cbx_operadoraCloseUp(Sender: TObject);
begin
  inherited;
  p_proprietario_id := -1;
  {lista as máquinas da operadora para o cbx_maquinas}
  qry_maquina.Close;
  qry_maquina.Params[0].AsInteger := cbx_operadora.KeyValue;
  qry_maquina.open;

  if qry_maquina.FieldByName('maquina_da_empresa').AsString = 'S' then
  begin
    {se for maquininha da empresa, calcula as taxas e valor liquido}
    prc_busca_taxa;
    prc_calcula_valor_liquido;

  end else
  if qry_maquina.FieldByName('maquina_da_empresa').AsString = 'N' then
  begin
    {se for maquina de terceiro "algum fornecedor", não cobra taxa e mantém o valor
    total da venda como sendo valor liquido}

    {valor liquido}
    edt_taxa.Text       := '0,00';
    edt_vr_liquido.Text := edt_vr_venda.Text;
    p_proprietario_id   := qry_maquina.FieldByName('proprietario_id').AsInteger;

  end;
end;

procedure TfrmCartaoVendasE.cbx_operadoraExit(Sender: TObject);
begin
  p_nm_operadora := cbx_operadora.Text;
end;

procedure TfrmCartaoVendasE.edt_vr_vendaExit(Sender: TObject);
begin
  inherited;
  prc_calcula_valor_liquido ;

  prc_calcular_parcelas ( qry_maquina.FieldByName('maquina_da_empresa').AsString );

end;

procedure TfrmCartaoVendasE.edt_vr_vendaKeyPress(Sender: TObject;
  var Key: Char);
begin
  inherited;
  ubiblioteca.prc_somente_numeros(sender,key);
end;

procedure TfrmCartaoVendasE.prc_calcula_valor_liquido;
begin
  {valor liquido}
  edt_vr_liquido.Text := FormatFloat('0.00',  strtofloat(edt_vr_venda.Text) - (strtofloat(edt_vr_venda.Text) * p_taxa /100) );
end;

procedure TfrmCartaoVendasE.FormCreate(Sender: TObject);
begin
  inherited;
  Tabela := 'CARTAO_VENDAS';
  p_proprietario_id := -1;
  p_somente_leitura := false;
end;

procedure TfrmCartaoVendasE.FormShow(Sender: TObject);
begin
  inherited;
  prc_componentes;
  prc_inicializar;

end;

procedure TfrmCartaoVendasE.prc_componentes;
begin
  {qry de principal}
  qry.Connection := Conexao;

  {qry operadoras}
  qry_operadora.Connection := Conexao;
  qry_operadora.SQL.Clear;
  qry_operadora.SQL.Add('select p.id, p.nome from pessoas p, cartao_operadoras o where p.id = o.pessoa_id order by nome');
  qry_operadora.Open;

  {qry máquina}
  qry_maquina.Connection := Conexao;
  qry_maquina.SQL.Clear;
  qry_maquina.SQL.Add('select ');
  qry_maquina.SQL.Add('  m.id, m.nr_maquina, p.nome, m.maquina_em_posse_de, m.maquina_da_empresa, m.proprietario_id ');
  qry_maquina.SQL.Add('from ');
  qry_maquina.SQL.Add('  cartao_maquinas m, pessoas p ');
  qry_maquina.SQL.Add('where ');
  qry_maquina.SQL.Add('  m.proprietario_id = p.id and m.cartao_operadoras_id = :pessoa_id' );

  {qry bandeiras}
  qry_bandeiras.Connection := Conexao;
  qry_bandeiras.SQL.Add('select id, descricao from cartao_bandeiras order by descricao');
  qry_bandeiras.Open;

  {qry formas de pagamento}
  qry_forma_pagto.Connection := Conexao;
  qry_forma_pagto.Open;

  {qry vendedores}
  qry_vendedor.Connection := Conexao;
  qry_vendedor.SQL.Add('select p.id, p.nome from pessoas p, vendedores v where p.id = v.pessoa_id order by p.nome');
  qry_vendedor.Open;

  gb_parcelas.Visible := false;

end;


procedure TfrmCartaoVendasE.prc_inicializar;
begin
  pnTitulo.Caption := 'VENDA COM CARTÃO DE CRÉDITO';

  case self.Operacao of
  uTipos.opIncluir: begin

                       lbl_sub_titulo.Caption    := 'INCLUSÃO';
                       lbl_Id.Caption            := '-1';
                       lbl_cadastrado_em.Caption := datetostr(date);
                       dtp_data_venda.Date       := date;
                       btnOk.Caption             := 'Incluir';

                     end;
  uTipos.opAlterar: begin

                      prc_ler_dados;
                      lbl_sub_titulo.Caption    := 'ALTERAÇÃO';
                      btnOk.Caption             := 'Alterar';
                      lbl_alterado_em.Caption   := datetostr(date);
                      btnOk.SetFocus;

                    end;
  uTipos.opExcluir: begin

                      prc_ler_dados;
                      lbl_sub_titulo.Caption := 'EXCLUIR';
                      pnDados.Enabled        := false;
                      btnOk.Caption          := 'Excluir';
                      btnFechar.SetFocus;

                    end;
  else
    begin
      pnDados.Enabled   := false;
      if btnFechar.CanFocus then btnFechar.SetFocus;
    end;
  end;

end;

procedure TfrmCartaoVendasE.prc_ler_dados;
var
 loQry: TFDQuery;
begin

  try

    loQry := TFDQuery.Create(Self);
    loQry.Connection := Conexao;;
    try
      loQry.sql.Add('SELECT * FROM ' + Tabela + '  WHERE ID =:ID');
      loQry.ParamByName('ID').AsInteger := Codigo;
      loQry.Open;
      if not loQry.IsEmpty then
      begin
        //Carrega dados tabela taxa
        lbl_Id.Caption            := loQry.FieldByName('ID').AsString;
        lbl_cadastrado_em.Caption := loQry.FieldByName('CADASTRADO_EM').AsString;
        lbl_alterado_em.Caption   := loQry.FieldByName('ALTERADO_EM').AsString;
        dtp_data_venda.Date       := loQry.FieldByName('DATA_VENDA').AsDateTime;

        cbx_operadora.KeyValue     := loQry.FieldByName('CARTAO_OPERADORA_ID').AsInteger;
        cbx_operadoraCloseUp(nil);
        cbx_nr_maquina.KeyValue    := loQry.FieldByName('CARTAO_MAQUINA_ID').AsInteger;
        cbx_desc_bandeira.KeyValue := loQry.FieldByName('CARTAO_BANDEIRA_ID').AsInteger;
        cbx_desc_forma_pagamento.KeyValue := loQry.FieldByName('CARTAO_FORMA_PAGTO_ID').AsInteger;

        edt_taxa.Text       := FormatFloat('0.00', loQry.FieldByName('TAXA').AsFloat);
        edt_vr_venda.Text   := FormatFloat('0.00', loQry.FieldByName('VALOR_VENDA').AsFloat);
        edt_vr_liquido.Text := FormatFloat('0.00', loQry.FieldByName('VALOR_LIQUIDO').AsFloat);
        edt_cv.Text         := loQry.FieldByName('CV').AsString;

        //cbx_vendedor.KeyValue := loQry.FieldByName('VENDEDOR_ID').AsInteger;



      end
      else
      begin
        ShowMessage('conta NÃO encontrada!');

      end;
    finally
      FreeAndNil(loQry);
    end;
  except
    on E : Exception do
       Raise Exception.Create(E.Message + Self.Name + '.prc_ler_dados');
  end;
end;

function TfrmCartaoVendasE.fnc_validar: boolean;
begin
  result := false;

  if cbx_operadora.Text = '' then
  begin
    criarmensagem('AVISO','Informe uma operadora');
    cbx_operadora.DropDown;
    exit;
  end;

  if cbx_nr_maquina.Text = '' then
  begin
    criarmensagem('AVISO','Informe uma maquininha');
    cbx_nr_maquina.DropDown;
    exit;
  end;

  if cbx_desc_bandeira.Text = '' then
  begin
    criarmensagem('AVISO','Informe uma bandeira');
    cbx_desc_bandeira.DropDown;
    exit;
  end;

  if cbx_desc_forma_pagamento.Text = '' then
  begin
    criarmensagem('AVISO','Informe uma forma de pagamento');
    cbx_desc_forma_pagamento.DropDown;
    exit;
  end;

  if strtofloatdef( edt_vr_venda.Text,0) = 0 then
  begin
    criarmensagem('AVISO','Informe um valor válido');
    edt_vr_venda.SetFocus;
    exit;
  end;

  if edt_cv.Text = '' then
  begin
    criarmensagem('AVISO','Informe um CV');
    edt_cv.SetFocus;
    exit;
  end;


  result := true;
end;

procedure TfrmCartaoVendasE.prc_busca_taxa;
var
  loQry : TFDQuery;
begin

  try
    {primeira pesquisa na tabela pai "taxas"}
    loQry := TFDQuery.Create(self);
    loQry.Connection := conexao;
    loqry.SQL.Clear;
    loqry.SQL.Add('select ');
    loqry.SQL.Add('  t.id, t.banco_id, t.contas_bancarias_itens_id, i.taxa, i.primeiro_vencimento ');
    loqry.SQL.Add('from                   ');
    loqry.SQL.Add('  cartao_taxas t,      ');
    loqry.SQL.Add('  cartao_operadoras o, ');
    loqry.SQL.Add('  cartao_bandeiras b,  ');
    loqry.SQL.Add('  cartao_taxas_itens i ');

    loqry.SQL.Add('where                                              ');
    loqry.SQL.Add('  t.id = i.cartao_taxas_id and                     ');
    //loqry.SQL.Add('  t.cartao_operadoras_id = o.id and ');
    loqry.SQL.Add('  t.cartao_operadoras_id = o.pessoa_id and         ');
    loqry.SQL.Add('  t.cartao_bandeiras_id  = b.id and                ');
    loqry.SQL.Add('  t.cartao_operadoras_id =:cartao_operadora_id and ');
    loqry.SQL.Add('  t.cartao_bandeiras_id  =:cartao_bandeiras_id and ');
    loqry.SQL.Add('  i.formas_pagto_id      =:forma_pagto_id          ');

    {passagem de parametros}
    loqry.Params.ParamByName('cartao_operadora_id').AsInteger := ubiblioteca.SeSenao( cbx_operadora.Text <> '' , cbx_operadora.KeyValue, -1) ;
    loqry.Params.ParamByName('cartao_bandeiras_id').AsInteger := ubiblioteca.SeSenao( cbx_desc_bandeira.Text <> '' , cbx_desc_bandeira.KeyValue, -1) ;
    loqry.Params.ParamByName('forma_pagto_id').AsInteger      := ubiblioteca.SeSenao( cbx_desc_forma_pagamento.Text <> '' , cbx_desc_forma_pagamento.KeyValue, -1) ;

    loqry.Open;

    if not loqry.IsEmpty then
    begin

      {localiza a conta pai, e pega o id, banco e conta onde cairão os créditos e a taxa}
      p_banco_id := loQry.FieldByName('banco_id').AsInteger;
      p_conta_id := loQry.FieldByName('contas_bancarias_itens_id').AsInteger;
      p_taxa     := loQry.FieldByName('taxa').Asfloat;
      p_primeiro_vencimento := loQry.FieldByName('primeiro_vencimento').AsInteger;

      {taxa da operação}
      edt_taxa.Text := FormatFloat('0.00', p_taxa);
    end
    else
    begin
      ShowMessage('Taxa de cartão não esta cadastrada no sistema.');
    end;


  finally
    FreeAndNil(loQry);
  end;
end;

procedure TfrmCartaoVendasE.prc_calcular_parcelas(maquina_empresa: string);
var
  valor_da_parcela, v, dif: double;
  i, qtde_parcelas: integer;
  duas_casas: string;
  vencimento : TDate;
begin
  //ShowMessage(qry_maquina.FieldByName('maquina_da_empresa').AsString) ;

  {limpa do dataset;}
  while not mtb_parcelas.Eof do mtb_parcelas.Delete;

  if maquina_empresa = 'S' then
  begin
    qtde_parcelas    := qry_forma_pagto.FieldByName('qtde_parcelas').AsInteger;
    valor_da_parcela :=  strtofloat (edt_vr_liquido.text) / qtde_parcelas;
    duas_casas       := formatfloat('0.00', valor_da_parcela);
    valor_da_parcela := strtofloat( duas_casas);
    vencimento       := dtp_data_venda.Date;

    {verifica se os valores batem }
    v   := valor_da_parcela * qtde_parcelas;
    dif := strtofloat (edt_vr_liquido.text) - v;

    i := 0;
    {máquininha da própria empresas}
    while i < qtde_parcelas do
    begin

      vencimento := vencimento + p_primeiro_vencimento;

      mtb_parcelas.Insert;
      mtb_parcelas.FieldByName('data').AsDateTime := vencimento;

      if i = 0 then
        mtb_parcelas.FieldByName('valor').AsFloat   := valor_da_parcela + dif
      else
        mtb_parcelas.FieldByName('valor').AsFloat   := valor_da_parcela;

      mtb_parcelas.post;


      inc(i);
    end;

  end
  else
  begin
    {maquininha de outros fornecedores}
    valor_da_parcela := strtofloat (edt_vr_venda.text);
    mtb_parcelas.Insert;
    mtb_parcelas.FieldByName('data').AsDateTime := dtp_data_venda.Date;
    mtb_parcelas.FieldByName('valor').AsFloat   := valor_da_parcela;
    mtb_parcelas.post;


  end;
end;

end.
