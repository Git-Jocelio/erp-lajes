unit ufrmCartaoFormasPagtoE;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, ufrmBaseEdicao, FireDAC.Stan.Intf,
  FireDAC.Stan.Option, FireDAC.Stan.Param, FireDAC.Stan.Error, FireDAC.DatS,
  FireDAC.Phys.Intf, FireDAC.DApt.Intf, FireDAC.Stan.Async, FireDAC.DApt,
  Data.DB, FireDAC.Comp.DataSet, FireDAC.Comp.Client, Vcl.StdCtrls, Vcl.Buttons,
  Vcl.ExtCtrls, uTipos, uBiblioteca;

type
  TfrmCartaoFormasPagtoE = class(TfrmBaseEdicao)
    Label2: TLabel;
    lbl_Id: TLabel;
    rg_tipo: TRadioGroup;
    rg_parcelas: TRadioGroup;
    cb_ativo: TCheckBox;
    procedure FormCreate(Sender: TObject);
    procedure FormShow(Sender: TObject);
    procedure rg_tipoClick(Sender: TObject);
    procedure btnOkClick(Sender: TObject);

  private
    FOperacao: uTipos.TOperacao;
    FTabela: string;
    FCodigo: integer;

    procedure prc_salvar;
    procedure prc_incluir_alterar(operacao: TOperacao);


  protected
    procedure prc_componentes;
    procedure prc_inicializar;
    procedure prc_ler_dados;
    function prc_validar: boolean;

  public
    property Codigo   :integer read FCodigo write FCodigo;
    property Operacao :uTipos.TOperacao read FOperacao write FOperacao;
    property Tabela   :string read FTabela write FTabela;

  end;

  procedure prc_incluir;
  procedure prc_alterar(ACodigo :integer);
  procedure prc_excluir(ACodigo :integer);

implementation

procedure prc_incluir;
var
  loForm : TfrmCartaoFormasPagtoE;
begin
  loForm := TfrmCartaoFormasPagtoE.Create(Application);
  try
    loForm.Operacao := uTipos.opIncluir;
    loForm.Codigo   := 0;
    loForm.ShowModal;
  finally
    FreeAndNil(loForm);
  end;
end;

procedure prc_alterar(ACodigo :integer);
var
  loForm : TfrmCartaoFormasPagtoE;
begin
  loForm := TfrmCartaoFormasPagtoE.Create(Application);
  try
    loForm.Operacao := uTipos.opAlterar;
    loForm.Codigo   := ACodigo;
    loForm.ShowModal;
  finally
    FreeAndNil(loForm);
  end;
end;

procedure prc_excluir(ACodigo :integer);
var
  loForm : TfrmCartaoFormasPagtoE;
begin
  loForm := TfrmCartaoFormasPagtoE.Create(Application);
  try
    loForm.Operacao := uTipos.opExcluir;
    loForm.Codigo   := ACodigo;
    loForm.ShowModal;
  finally
    FreeAndNil(loForm);
  end;
end;


{$R *.dfm}

procedure TfrmCartaoFormasPagtoE.btnOkClick(Sender: TObject);
begin
  inherited;
  prc_salvar;

end;

procedure TfrmCartaoFormasPagtoE.FormCreate(Sender: TObject);
begin
  inherited;
  Tabela := 'CARTAO_FORMAS_PAGTO';

end;

procedure TfrmCartaoFormasPagtoE.FormShow(Sender: TObject);
begin
  inherited;
  prc_componentes;
  prc_inicializar;

end;

procedure TfrmCartaoFormasPagtoE.prc_componentes;
begin
  {qry de principal}
  qry.Connection := Conexao;

end;

procedure TfrmCartaoFormasPagtoE.prc_incluir_alterar(operacao: TOperacao);
var
  descricao : string;
begin
  qry.sql.clear;
  if operacao = opExcluir then
  begin
    qry.SQL.Add('update '+ self.Tabela + ' set ATIVO = ''N'' where id =:id');
    qry.ParamByName('id').AsInteger := Codigo;
    qry.ExecSQL;
    exit;
  end;


  if operacao = OpIncluir then
  begin
    qry.SQL.Add('insert into ' + self.Tabela );
    qry.SQL.Add('(');
    qry.SQL.Add('  DESCRICAO,         ');
    qry.SQL.Add('  DEBITO,            ');
    qry.SQL.Add('  CREDITO,           ');
    qry.SQL.Add('  QTDE_PARCELAS,     ');
    qry.SQL.Add('  PARCELADO_EMISSOR, ');
    qry.SQL.Add('  PARCELADO_LOJA,    ');
    qry.SQL.Add('  ATIVO              ');
    qry.SQL.Add(')                    ');
    qry.SQL.Add('VALUES               ');
    qry.SQL.Add('(                    ');
    qry.SQL.Add(' :DESCRICAO,         ');
    qry.SQL.Add(' :DEBITO,            ');
    qry.SQL.Add(' :CREDITO,           ');
    qry.SQL.Add(' :QTDE_PARCELAS,     ');
    qry.SQL.Add(' :PARCELADO_EMISSOR, ');
    qry.SQL.Add(' :PARCELADO_LOJA,    ');
    qry.SQL.Add(' :ATIVO              ');
    qry.SQL.Add(')                    ');

  end
  else
  begin
    qry.SQL.Add('UPDATE                                    ');
    qry.SQL.Add(Tabela);
    qry.SQL.Add('SET                                       ');
    qry.SQL.Add('  DESCRICAO = :DESCRICAO,                 ');
    qry.SQL.Add('  DEBITO = :DEBITO,                       ');
    qry.SQL.Add('  CREDITO = :CREDITO,                     ');
    qry.SQL.Add('  QTDE_PARCELAS = :QTDE_PARCELAS,         ');
    qry.SQL.Add('  PARCELADO_EMISSOR = :PARCELADO_EMISSOR, ');
    qry.SQL.Add('  PARCELADO_LOJA = :PARCELADO_LOJA,       ');
    qry.SQL.Add('  ATIVO = :ATIVO                          ');
    qry.SQL.Add('WHERE                                     ');
    qry.SQL.Add('  ID = :ID                                ');
    //
    qry.ParamByName('ID').AsInteger := Codigo;

  end;
  //ShowMessage(QRY.SQL.Text);

  {descrição da forma de pagamento}
  if rg_tipo.ItemIndex = 0 then
    descricao := 'DÉBITO';
  if rg_tipo.ItemIndex = 1 then
    descricao := 'CREDITO À VISTA';
  if rg_tipo.ItemIndex = 2 then
    descricao := 'CREDITO PARCELADO ( ' + inttostr(rg_parcelas.ItemIndex + 2) + 'X )';

  qry.ParamByName('DESCRICAO').AsString      := descricao;
  qry.ParamByName('DEBITO').AsString         := ubiblioteca.SeSenao(rg_tipo.ItemIndex = 0,'S','N');
  qry.ParamByName('CREDITO').AsString        := ubiblioteca.SeSenao(rg_tipo.ItemIndex > 0,'S','N');
  qry.ParamByName('QTDE_PARCELAS').AsInteger := rg_parcelas.ItemIndex + 2;
  qry.ParamByName('PARCELADO_EMISSOR').AsString := 'N';
  qry.ParamByName('PARCELADO_LOJA').AsString    := 'N';
  qry.ParamByName('ATIVO').AsString := ubiblioteca.SeSenao(cb_ativo.Checked  , 'S','N');

  qry.ExecSQL;

end;

procedure TfrmCartaoFormasPagtoE.prc_inicializar;
begin
  pnTitulo.Caption := 'CONFIGURAÇÃO DE FORMA DE PAGAMENTO DE CARTÃO DE CRÉDITO';

  case self.Operacao of
  uTipos.opIncluir: begin

                       lbl_sub_titulo.Caption := 'INCLUSÃO';
                       pnDados.Enabled := true;

                       lbl_Id.Caption := '-1';
                       btnOk.Caption := 'Incluir';

                     end;
  uTipos.opAlterar: begin

                      prc_ler_dados;
                      lbl_sub_titulo.Caption := 'ALTERAÇÃO';
                      btnOk.Caption    := 'Alterar';
                      //
                      btnOk.SetFocus;

                    end;
  uTipos.opExcluir: begin

                      prc_ler_dados;
                      lbl_sub_titulo.Caption := 'EXCLUIR';
                      pnDados.Enabled     := false;
                      btnOk.Caption       := 'Excluir';
                      btnFechar.SetFocus;

                    end;
  else
    begin
      pnDados.Enabled   := false;
      if btnFechar.CanFocus then btnFechar.SetFocus;
    end;
  end;

end;

procedure TfrmCartaoFormasPagtoE.prc_ler_dados;
var
 loQry: TFDQuery;
begin

  try

    loQry := TFDQuery.Create(Self);
    try
      with loQry, loQry.Sql do
      begin
        Connection := Self.Conexao;
        Add('SELECT * FROM ' + Self.Tabela + ' WHERE ID =:ID');
        ParamByName('ID').AsInteger := Codigo;
        Open;
      end;
      if not loQry.IsEmpty then
      begin
        //Carrega dados
        //tipo
        lbl_Id.Caption     := loQry.FieldByName('ID').AsString;
        if loQry.FieldByName('DEBITO').AsString = 'S' then
          rg_tipo.ItemIndex := 0;

        if loQry.FieldByName('CREDITO').AsString = 'S' then
          if loQry.FieldByName('QTDE_PARCELAS').AsInteger = 1 then
            rg_tipo.ItemIndex := 1
          else
            rg_tipo.ItemIndex := 2;

        // qtde parcelas
        if loQry.FieldByName('CREDITO').AsString = 'S' then
          rg_parcelas.ItemIndex := loQry.FieldByName('QTDE_PARCELAS').AsInteger -2;

        cb_ativo.Checked   := loQry.FieldByName('ATIVO').AsString = 'S';

      end
      else
      begin
        ShowMessage('conta NÃO encontrada!');

      end;
    finally
      FreeAndNil(loQry);
    end;
  except
    on E : Exception do
       Raise Exception.Create(E.Message + Self.Name + '.prc_ler_dados');
  end;
end;

procedure TfrmCartaoFormasPagtoE.prc_salvar;
begin
  if not prc_validar then Exit;

  if not Self.Conexao.InTransaction then Self.Conexao.StartTransaction;
  //***
  try
    prc_incluir_alterar(Operacao);
    if Self.Conexao.InTransaction then Self.Conexao.Commit;
    ModalResult := mrOk;
  except
    conexao.Rollback;
    ShowMessage('Não foi possível salvar o registro');
  end;

end;

function TfrmCartaoFormasPagtoE.prc_validar: boolean;
begin
  result := false;

  if rg_tipo.ItemIndex = -1 then
  begin
    ShowMessage('Informe o tipo de cartão');
    rg_tipo.SetFocus;
    exit;
  end;

  if rg_tipo.ItemIndex = 2 then
    if rg_parcelas.ItemIndex = -1 then
    begin
      ShowMessage('Informe a quantidade de parcelas');
      rg_parcelas.SetFocus;
      exit;
    end;

  result := true;
end;

procedure TfrmCartaoFormasPagtoE.rg_tipoClick(Sender: TObject);
begin
  inherited;
  rg_parcelas.Enabled := rg_tipo.ItemIndex = 2;

  if rg_tipo.ItemIndex <> 2 then
    rg_parcelas.ItemIndex := -1;
end;

end.
