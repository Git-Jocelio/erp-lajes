unit ufrmCartaoOperadorasE;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, ufrmBaseEdicao, FireDAC.Stan.Intf,
  FireDAC.Stan.Option, FireDAC.Stan.Param, FireDAC.Stan.Error, FireDAC.DatS,
  FireDAC.Phys.Intf, FireDAC.DApt.Intf, FireDAC.Stan.Async, FireDAC.DApt,
  Data.DB, FireDAC.Comp.DataSet, FireDAC.Comp.Client, Vcl.StdCtrls, Vcl.Buttons,
  Vcl.ExtCtrls, uTipos, uBiblioteca;

type
  TfrmCartaoOperadorasE = class(TfrmBaseEdicao)
    Label2: TLabel;
    lbl_Id: TLabel;
    Label1: TLabel;
    lbl_cadastrado_em: TLabel;
    Label4: TLabel;
    lbl_alterado_em: TLabel;
    cb_ativo: TCheckBox;
    Label6: TLabel;
    edt_nome_operadora: TEdit;
    Label3: TLabel;
    edt_nr_estabelecimento: TEdit;
    Label5: TLabel;
    edt_telefones: TEdit;
    edt_email: TEdit;
    Label8: TLabel;
    edt_senha: TEdit;
    Label7: TLabel;

    procedure FormCreate(Sender: TObject);
    procedure FormShow(Sender: TObject);

    procedure btnOkClick(Sender: TObject);
  private
    FOperacao: uTipos.TOperacao;
    FTabela: string;
    FCodigo: integer;

    procedure prc_salvar;
    procedure prc_incluir_alterar(operacao: TOperacao);

  protected
    procedure prc_componentes;
    procedure prc_inicializar;
    procedure prc_ler_dados;
    function prc_validar: boolean;

  public
    property Codigo   :integer read FCodigo write FCodigo;
    property Operacao :uTipos.TOperacao read FOperacao write FOperacao;
    property Tabela   :string read FTabela write FTabela;

  end;

  procedure prc_incluir;
  procedure prc_alterar(ACodigo :integer);
  procedure prc_excluir(ACodigo :integer);

implementation

procedure prc_incluir;
var
  loForm : TfrmCartaoOperadorasE;
begin
  loForm := TfrmCartaoOperadorasE.Create(Application);
  try
    loForm.Operacao := uTipos.opIncluir;
    loForm.Codigo   := 0;
    loForm.ShowModal;
  finally
    FreeAndNil(loForm);
  end;
end;

procedure prc_alterar(ACodigo :integer);
var
  loForm : TfrmCartaoOperadorasE;
begin
  loForm := TfrmCartaoOperadorasE.Create(Application);
  try
    loForm.Operacao := uTipos.opAlterar;
    loForm.Codigo   := ACodigo;
    loForm.ShowModal;
  finally
    FreeAndNil(loForm);
  end;
end;

procedure prc_excluir(ACodigo :integer);
var
  loForm : TfrmCartaoOperadorasE;
begin
  loForm := TfrmCartaoOperadorasE.Create(Application);
  try
    loForm.Operacao := uTipos.opExcluir;
    loForm.Codigo   := ACodigo;
    loForm.ShowModal;
  finally
    FreeAndNil(loForm);
  end;
end;


{$R *.dfm}

procedure TfrmCartaoOperadorasE.btnOkClick(Sender: TObject);
begin
  inherited;
  prc_salvar;
end;

procedure TfrmCartaoOperadorasE.FormCreate(Sender: TObject);
begin
  inherited;
  Tabela := 'CARTAO_OPERADORAS';

end;

procedure TfrmCartaoOperadorasE.FormShow(Sender: TObject);
begin
  inherited;
  prc_componentes;
  prc_inicializar;

end;

procedure TfrmCartaoOperadorasE.prc_componentes;
begin
  {qry de principal}
  qry.Connection := Conexao;

end;

procedure TfrmCartaoOperadorasE.prc_incluir_alterar(operacao: TOperacao);
begin
  qry.sql.clear;
  if operacao = opExcluir then
  begin
    qry.SQL.Add('update '+ self.Tabela + ' set ATIVO = ''N'' where id =:id');
    qry.ParamByName('id').AsInteger := Codigo;
    qry.ExecSQL;
    exit;
  end;


  if operacao = OpIncluir then
  begin
    qry.SQL.Add('insert into ' + self.Tabela );
    qry.SQL.Add('(');
    //qry.SQL.Add('  ID, ');
    qry.SQL.Add('  CADASTRADO_EM,      ');
    qry.SQL.Add('  DESCRICAO,          ');
    qry.SQL.Add('  NR_ESTABELECIMENTO, ');
    qry.SQL.Add('  EMAIL,              ');
    qry.SQL.Add('  TEL_CONTATO,        ');
    qry.SQL.Add('  ATIVO,             ');
    qry.SQL.Add('  SENHA               ');
    qry.SQL.Add(')                     ');
    qry.SQL.Add('VALUES                ');
    qry.SQL.Add('(                     ');
    //qry.SQL.Add('  :ID, ');
    qry.SQL.Add('  :CADASTRADO_EM,     ');
    qry.SQL.Add('  :DESCRICAO,         ');
    qry.SQL.Add('  :NR_ESTABELECIMENTO,');
    qry.SQL.Add('  :EMAIL,             ');
    qry.SQL.Add('  :TEL_CONTATO,       ');
    qry.SQL.Add('  :ATIVO,            ');
    qry.SQL.Add('  :SENHA              ');
    qry.SQL.Add(')                     ');
   // qry.SQL.Add('returning ID          ');

    qry.ParamByName('CADASTRADO_EM').AsDate := Date;
  end
  else
  begin
    qry.SQL.Add('UPDATE                                     ');
    qry.SQL.Add(Tabela);
    qry.SQL.Add('SET                                        ');
    qry.SQL.Add('  ALTERADO_EM = :ALTERADO_EM,              ');
    qry.SQL.Add('  DESCRICAO = :DESCRICAO,                  ');
    qry.SQL.Add('  NR_ESTABELECIMENTO = :NR_ESTABELECIMENTO,');
    qry.SQL.Add('  EMAIL = :EMAIL,                          ');
    qry.SQL.Add('  TEL_CONTATO = :TEL_CONTATO,              ');
    qry.SQL.Add('  ATIVO = :ATIVO,                          ');
    qry.SQL.Add('  SENHA = :SENHA                           ');
    qry.SQL.Add('WHERE                                      ');
    qry.SQL.Add('  ID = :ID                                 ');
    //
    qry.ParamByName('ID').AsInteger       := Codigo;
    qry.ParamByName('ALTERADO_EM').AsDate := Date;

  end;
  //ShowMessage(QRY.SQL.Text);

  qry.ParamByName('DESCRICAO').AsString          := edt_nome_operadora.Text;
  qry.ParamByName('NR_ESTABELECIMENTO').AsString := edt_nr_estabelecimento.Text;
  qry.ParamByName('EMAIL').AsString              := edt_email.Text;
  qry.ParamByName('TEL_CONTATO').AsString        := edt_telefones.Text;
  qry.ParamByName('SENHA').AsString              := edt_senha.Text;
  qry.ParamByName('ATIVO').AsString              := ubiblioteca.SeSenao(cb_ativo.Checked  , 'S','N');

  qry.ExecSQL;
  (*
  //exemplo de como pegar o id do registro gerado
  if operacao = OpIncluir then
  begin
    qry.Open;
    Codigo := qry.FieldByName('id').AsInteger;

  end
  else
  begin
    qry.ExecSQL;
  end;
  *)
end;

procedure TfrmCartaoOperadorasE.prc_inicializar;
begin

  pnTitulo.Caption := 'CADASTRO DE OPERADORAS DE CARTÃO DE CRÉDITO';

  case self.Operacao of
  uTipos.opIncluir: begin

                       lbl_sub_titulo.Caption := 'INCLUSÃO';
                       pnDados.Enabled := true;

                       lbl_Id.Caption := '-1';
                       lbl_cadastrado_em.Caption := DateToStr(Date);
                       btnOk.Caption := 'Incluir';

                     end;
  uTipos.opAlterar: begin

                      prc_ler_dados;
                      lbl_sub_titulo.Caption := 'ALTERAÇÃO';
                      btnOk.Caption    := 'Alterar';
                      //
                      btnOk.SetFocus;

                    end;
  uTipos.opExcluir: begin

                      prc_ler_dados;
                      lbl_sub_titulo.Caption := 'EXCLUIR';
                      pnDados.Enabled     := false;
                      btnOk.Caption       := 'Excluir';
                      btnFechar.SetFocus;

                    end;
  else
    begin
      pnDados.Enabled   := false;
      if btnFechar.CanFocus then btnFechar.SetFocus;
    end;
  end;

end;

procedure TfrmCartaoOperadorasE.prc_ler_dados;
var
 loQry: TFDQuery;
begin

  try

    loQry := TFDQuery.Create(Self);
    try
      with loQry, loQry.Sql do
      begin
        Connection := Self.Conexao;
        Add('SELECT * FROM ' + Self.Tabela + ' WHERE ID =:ID');
        ParamByName('ID').AsInteger := Codigo;
        Open;
      end;
      if not loQry.IsEmpty then
      begin
        //Carrega dados da pessoas
        lbl_Id.Caption             := loQry.FieldByName('ID').AsString;
        lbl_cadastrado_em.Caption  := loQry.FieldByName('CADASTRADO_EM').AsString;
        lbl_alterado_em.Caption    := loQry.FieldByName('ALTERADO_EM').AsString;
        cb_ativo.Checked           := loQry.FieldByName('ATIVO').AsString = 'S';
        edt_nome_operadora.Text    := loQry.FieldByName('DESCRICAO').AsString;
        edt_nr_estabelecimento.Text:= loQry.FieldByName('NR_ESTABELECIMENTO').AsString;
        edt_email.Text             := loQry.FieldByName('EMAIL').AsString;
        edt_telefones.Text         := loQry.FieldByName('TEL_CONTATO').AsString;
        edt_senha.Text             := loQry.FieldByName('SENHA').AsString;

      end
      else
      begin
        ShowMessage('conta NÃO encontrada!');

      end;
    finally
      FreeAndNil(loQry);
    end;
  except
    on E : Exception do
       Raise Exception.Create(E.Message + Self.Name + '.prc_ler_dados');
  end;
end;

procedure TfrmCartaoOperadorasE.prc_salvar;
begin
  if not prc_validar then Exit;

  if not Self.Conexao.InTransaction then Self.Conexao.StartTransaction;
  //***
  try
    prc_incluir_alterar(Operacao);
    if Self.Conexao.InTransaction then Self.Conexao.Commit;
    ModalResult := mrOk;
  except
    conexao.Rollback;
    ShowMessage('Não foi possível salvar o registro');
  end;

end;

function TfrmCartaoOperadorasE.prc_validar: boolean;
begin
  result := false;

  if edt_nome_operadora.text = '' then
  begin
    ShowMessage('informe o NOME DA OPERADORA DE CARTÃO DE CRÉDITO');
    edt_nome_operadora.SetFocus;
    exit;
  end;

  result := true;
end;

end.
